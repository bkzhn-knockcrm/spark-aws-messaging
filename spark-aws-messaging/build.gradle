plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group = 'com.fabiogouw'
archivesBaseName = 'spark-aws'
version = '0.2.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    compile 'org.apache.spark:spark-core_2.12:3.1.1'
    compile 'org.apache.spark:spark-sql_2.12:3.1.1'
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.991')
    implementation 'com.amazonaws:aws-java-sdk-sqs'
    implementation 'com.amazonaws:aws-java-sdk-sns'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

test {
    useJUnitPlatform()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Spark AWS'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'AWS SQS sink provider'
                url 'https://github.com/fabiogouw/spark-aws'

                scm {
                    connection 'scm:git:git://github.com/fabiogouw/spark-aws.git'
                    developerConnection 'scm:git:git@github.com:fabiogouw/spark-aws.git'
                    url 'https://github.com/fabiogouw/spark-aws'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'fabiogouw'
                        name 'Fabio Gouw'
                    }
                }
            }
        }
    }
}